diff --git a/nixos/modules/misc/ids.nix b/nixos/modules/misc/ids.nix
index cb75845..f5d7812 100644
--- a/nixos/modules/misc/ids.nix
+++ b/nixos/modules/misc/ids.nix
@@ -298,6 +298,14 @@
       minio = 280;
       kanboard = 281;
 
+      alias = 7790;
+      qmaild = 7791;
+      qmaill = 7792;
+      qmailp = 7793;
+      qmailq = 7794;
+      qmailr = 7795;
+      qmails = 7796;
+
       # When adding a uid, make sure it doesn't match an existing gid. And don't use uids above 399!
 
       nixbld = 30000; # start of range of uids
@@ -569,6 +577,8 @@
       # uids and gids. Also, don't use gids above 399!
 
       users = 100;
+      qmail = 2107;
+      nofiles = 2108;
       nixbld = 30000;
       nogroup = 65534;
     };
diff --git a/nixos/modules/module-list.nix b/nixos/modules/module-list.nix
index d8c7c7e..bf51543 100644
--- a/nixos/modules/module-list.nix
+++ b/nixos/modules/module-list.nix
@@ -271,6 +271,7 @@
   ./services/mail/postfix.nix
   ./services/mail/postsrsd.nix
   ./services/mail/postgrey.nix
+  ./services/mail/qmail.nix
   ./services/mail/spamassassin.nix
   ./services/mail/rspamd.nix
   ./services/mail/rmilter.nix
diff --git a/nixos/modules/services/mail/qmail.nix b/nixos/modules/services/mail/qmail.nix
new file mode 100644
index 0000000..63d3ca4
--- /dev/null
+++ b/nixos/modules/services/mail/qmail.nix
@@ -0,0 +1,201 @@
+{ config, pkgs, lib, ... }:
+
+with lib;
+
+let
+
+  cfg = config.runit.qmail;
+  pkg = pkgs.diet-qmail;
+  uids = config.ids.uids;
+  gids = config.ids.gids;
+
+  aliasOpts = { name, ... }: {
+    options = {
+      alias = mkOption {
+        example = "postmaster";
+        type = types.uniq types.string;
+        description = "Alias name";
+      };
+      real = mkOption {
+        example = "jsmith";
+        default = null;
+        type = types.nullOr types.string;
+        description = "Real user to receive mail.";
+      };
+      content = mkOption {
+        example = "anotheruser";
+        default = null;
+        type = types.nullOr types.string;
+        description = "Verbatim content of the alias file. See dot-qmail(5).";
+      };
+    };
+    config = {
+      alias = mkDefault name;
+    };
+  };
+
+  aliasF = alias: nameValuePair "qmail/alias/.qmail-${alias.alias}" {
+    text = if alias.real != null then alias.real else alias.content;
+  };
+
+in
+
+{
+
+  ###### interface
+
+  options = {
+
+    runit.qmail = {
+
+      enable = mkOption {
+        default = false;
+        description = "Whether to enable qmail service.";
+      };
+
+      defaultdelivery = mkOption {
+        default = "./Maildir/";
+        description = "Default delivery (mbox or maildir). See qmail-start(8).";
+      };
+
+      me = mkOption {
+        default = "";
+        description = "Fully qualified name of this host.";
+      };
+
+      defaultdomain = mkOption {
+        default = "";
+        description = "Default domain name. See qmail-inject(8).";
+      };
+
+      locals = mkOption {
+        default = [];
+        description = ''
+          List of domain names that the current host receives mail for. See
+          qmail-send(8).
+        '';
+      };
+
+      plusdomain = mkOption {
+        default = "";
+        description = ''
+          Plus domain name. qmail-inject adds this name to any host name that
+          ends with a plus sign. See qmail-inject(8).
+        '';
+      };
+
+      rcpthosts = mkOption {
+        default = [];
+        description = ''
+          List of domain names that the current host receives mail for. See
+          qmail-send(8).
+        '';
+      };
+
+      virtualdomains = mkOption {
+        default = [];
+        description = ''
+          List of virtual domains (domain:user).
+        '';
+      };
+
+      aliases = mkOption {
+        default = {};
+        type = types.loaOf types.optionSet;
+        example = {
+          root = { real = "jsmith"; };
+          postmaster = { content = "another user"; };
+        };
+        options = [ aliasOpts ];
+        description = ''
+          Global qmail aliases configuration. Addresses that don't start with a
+          username are controlled by a special user, alias. Delivery
+          instructions for foo go into ~alias/.qmail-foo; delivery instructions
+          for user-foo go into ~user/.qmail-foo. See dot-qmail(5) for the full
+          story or doc/INSTALL.alias in qmail installaction directory.
+        '';
+      };
+
+      log = mkOption {
+        default = {};
+      };
+
+    };
+  };
+
+
+  ###### implementation
+
+  config = mkIf cfg.enable {
+
+    environment.systemPackages = [ pkg ];
+
+    security.setuidOwners = [ {
+      program = "qmail-queue";
+      owner = "qmailq";
+      group = "qmail";
+      setuid = true;
+      setgid = false;
+    } ];
+
+    # groups
+    users.extraGroups = {
+      qmail.gid = gids.qmail;
+      nofiles.gid = gids.nofiles;
+    };
+
+    users.extraUsers = {
+      alias = {
+        uid = uids.alias;
+        group = "nofiles";
+        home = "/var/qmail/alias";
+        createHome = false;
+      };
+      qmaild = { uid = uids.qmaild; group = "nofiles"; };
+      qmaill = { uid = uids.qmaill; group = "nofiles"; };
+      qmailp = { uid = uids.qmailp; group = "nofiles"; };
+      qmailq = { uid = uids.qmailq; group = "qmail"; };
+      qmailr = { uid = uids.qmailr; group = "qmail"; };
+      qmails = { uid = uids.qmails; group = "qmail"; };
+    };
+
+    environment.etc = {
+      "qmail/control/me".text = cfg.me;
+      "qmail/control/defaultdomain".text = cfg.defaultdomain;
+      "qmail/control/locals".text = concatStringsSep "\n" cfg.locals;
+      "qmail/control/plusdomain".text = cfg.plusdomain;
+      "qmail/control/rcpthosts".text = concatStringsSep "\n" cfg.rcpthosts;
+      "qmail/control/virtualdomains".text = concatStringsSep "\n" cfg.virtualdomains;
+    } // listToAttrs (map aliasF (attrValues cfg.aliases));
+
+    runit.services.qmail = {
+
+      preRun = ''
+        #!${pkgs.bash}/bin/sh
+        qdir=/var/qmail/queue
+        cdir=/var/qmail/control
+        adir=/var/qmail/alias
+        bdir=/var/qmail/bin
+        [ -d $qdir ] || ${pkg}/bin/queue-fix $qdir
+        [ -e $cdir ] || ln -sf /etc/qmail/control $cdir
+        [ -e $adir ] || ln -sf /etc/qmail/alias $adir
+        [ -d $bdir ] || mkdir -p $bdir
+        [ -e $bdir/qmail-queue ] || ln -s /var/setuid-wrappers/qmail-queue $bdir/qmail-queue
+        for i in ${pkg}/bin/*; do
+          n=`basename $i`
+          [ -e $bdir/$n ] || ln -s /run/current-system/sw/bin/$n $bdir/$n
+        done
+      '';
+
+      cmd = "${pkg}/bin/qmail-start ${cfg.defaultdelivery}";
+
+      log = cfg.log;
+
+    };
+
+  };
+
+}
+
+# vim: et ts=2 sw=2 syn=conf 
+
